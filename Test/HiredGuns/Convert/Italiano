.key Figure

Echo "*N                 - [1mCONVERSIONE DELLE IMMAGINI DEI PERSONAGGI[22m -*N"

List >Ram:CopyFile1 "<Figure>" lformat "Copy Ram:MakeFigure/Figure.crn /CharactersGfx/%N.gfx"
List >Ram:CopyFile2 "<Figure>" lformat "Copy Ram:MakeFigure/Figure.crn *"Hired Guns Disk 1:CharactersGfx/%N.gfx*""
List >Ram:CopyFile3 "<Figure>" lformat "Copy Ram:MakeFigure/Figure.crn //Disk1/CharactersGfx/%N.gfx"

If "<Figure>" Eq ""
	Echo "     Evidenzia con un clic questa icona, poi tieni premuto Maiusc e fai*N     doppio clic sull'immagine che vuoi convertire.*N"
	Ask "               [32mPremi il tasto di RITORNO per continuare.[31m"
        EndCli
EndIf

If Not Exists Ram:MakeFigure
	Echo "               [3mI comandi vengono copiati su RAM:....[23m*N"
	Copy /MakeFigure Ram:MakeFigure quiet
	Copy /C/Crunch Ram:MakeFigure/Crunch quiet
EndIf
Path Ram:MakeFigure Add

Echo "     Questo script convertirà un'immagine in modo che potrà essere"
Echo "     usata in Hired Guns."
Echo "     Utenti di floppy disk: lo script tenterà di sovrascrivere il"
Echo "     disco 1, per cui assicuratevi di usare una copia di backup."
Echo "     Consultate nel manuale la sezione dedicata alla realizzazione"
Echo "     di copie di backup dei dischi di Hired Guns.*N"

Ask "               [32mVuoi avviare la conversione? ([31mY[32m/[31mN[32m)[31m"

If Warn
	Echo "*N               [3mL'immagine grafica viene copiata su RAM:....[23m"
	Copy "<Figure>" Ram:MakeFigure/Figure.ilbm quiet

	Echo "               [3mEstrazione grafica lato sinistro del personaggio....[23m"
	MakeFigure >nil: -@Ram:MakeFigure/Filters -#Left Ram:MakeFigure/Figure.ilbm Ram:MakeFigure/Left.bin
	Join Ram:MakeFigure/Figure.dump Ram:MakeFigure/Left.bin as Ram:MakeFigure/Left.bob

	Echo "               [3mEstrazione grafica lato destro del personaggio....[23m"
	MakeFigure >nil: -@Ram:MakeFigure/Filters -#Right Ram:MakeFigure/Figure.ilbm Ram:MakeFigure/Right.bin
	Join Ram:MakeFigure/Figure.dump Ram:MakeFigure/Right.bin as Ram:MakeFigure/Right.bob

	Echo "               [3mEstrazione grafica lato destro del personaggio....[23m"
	MakeFigure >nil: -@Ram:MakeFigure/Filters -#Front Ram:MakeFigure/Figure.ilbm Ram:MakeFigure/Front.bin
	Join Ram:MakeFigure/Figure.dump Ram:MakeFigure/Front.bin as Ram:MakeFigure/Front.bob

	Echo "               [3mEstrazione grafica lato frontale del personaggio....[23m"
	MakeFigure >nil: -@Ram:MakeFigure/Filters -#Back Ram:MakeFigure/Figure.ilbm Ram:MakeFigure/Back.bin
	Join Ram:MakeFigure/Figure.dump Ram:MakeFigure/Back.bin as Ram:MakeFigure/Back.bob

	Echo "               [3mEstrazione grafica del viso....[23m"
	MakeFigure >nil: -@Ram:MakeFigure/Filters -#Face Ram:MakeFigure/Figure.ilbm Ram:MakeFigure/Face.bin
	Join Ram:MakeFigure/Face.dump Ram:MakeFigure/Face.bin as Ram:MakeFigure/Face.bob

	Echo "               [3mCompressione del file (può richiedere tempo)....[23m"

	Join Ram:MakeFigure/Front.bob Ram:MakeFigure/Left.bob Ram:MakeFigure/Right.bob Ram:MakeFigure/Back.bob Ram:MakeFigure/Face.bob as Ram:MakeFigure/Figure.bob
	Crunch >nil: Ram:MakeFigure/Figure.bob ram:Makefigure/Figure.crn

	Echo "               [3mScrittura del file....[23m"

	If Exists //Disk1/CharactersGfx
		Execute Ram:CopyFile3
	Else
		If Exists /CharactersGfx
			Execute Ram:CopyFile1
		Else
			Execute Ram:CopyFile2
		EndIf
	EndIf

	Echo "*N               Conversione terminata!"
Else
	Echo "*N               Conversione annullata!"
EndIf
